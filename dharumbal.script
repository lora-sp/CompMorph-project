clear stack;

define Vowel [a|i|u];
define Cons [b|d|h|g|k|l|m|n|p|r|t|w|y]; 


#Nouns
#Case
#ErgInstr
define ErgInstrRU [A -> R || [l|u|a] _]; 
define ErgInstrNGU [A (->) n g || i _]; 
define ErgInstrNGGU [A -> n g g || i _]; 
define ErgInstrDel [A -> 0 || r r _]; 
define ErgInstrB [A -> b || m _]; 
define ErgInstrD [A -> d || n _];
define ErgInstrDH [A -> d h || n h _]; 
define ErgInstrG [A -> g || n g _];
#AccH
define AccHN [B (->) n ];
define AccHDel [B -> 0];
#Loc
define LocVowel [C -> n g || Vowel _]; 
define LocRA [C -> R || l _];
define LocDA [C -> d || [Cons - l] _ ];
#Abl
define AblNG [D -> n g || Vowel _]; 
define AblDel [D -> 0 || Cons _]; 


#Verbs
#Derivational Affixes
#Causative
define CausNHDHA [E (->) n h d h];
define CausNGGA [E (->) n g g];
define CausNGIGA [E (->) n g i g];
define CausNHAGA [E (->) n h a g];
define CausGA [E -> g];
#Reciprocal
define ReciBA [F (->) b];
define ReciLA [F -> l];
#Purposive (Zero Class)
define PurpZGU [G (->) g u];
define PurpZYI [G -> y i];
#Tense/Mood Suffixes
#Purposive (N Class)
define PurpNGU [H (->) g];
define PurpNDHAYU [H -> d h a y];
#Purposive (NG1 Class)
define PurpNG1GU [I (->) g];
define PurpNG1YU [I (->) y];
define PurpNG1YAYU [I -> y a y];
#Imperative (RR Class)
define ImpRRGA [J (->) g];
define ImpRRA [J -> 0];
#Imperative (NG1 Class)
define ImpNG1NGA [K (->) n];
define ImpNG1GA [K -> 0]; 


#morpho-phonemics:
define FinalL [l -> 0 || _ R];
define SuffR [R -> r];

read regex ErgInstrRU .o. ErgInstrNGU .o. ErgInstrNGGU 
.o. ErgInstrDel .o. ErgInstrB .o. ErgInstrD .o. ErgInstrDH
.o. ErgInstrG .o. AccHN .o. AccHDel .o. LocVowel .o. LocDA .o. LocRA
.o. AblNG .o. AblDel .o. CausNHDHA .o. CausNGGA .o. CausNGIGA
.o. CausNHAGA .o. CausGA .o. ReciBA .o. ReciLA
.o. PurpZGU .o. PurpZYI .o. PurpNGU .o. PurpNDHAYU 
.o. PurpNG1GU .o. PurpNG1YU .o. PurpNG1YAYU 
.o. ImpRRGA .o. ImpRRA .o. ImpNG1NGA .o. ImpNG1GA .o. FinalL .o. SuffR;

define Rules;

read lexc < lex-nouns.txt;
define Nouns;
read lexc < lex-verbs.txt;
define Verbs;

read regex Rules;
read regex [Nouns | Verbs];

compose net;

apply down < dharumbal-upper.txt;
apply up < dharumbal-lower.txt;



#source dharumbal.script